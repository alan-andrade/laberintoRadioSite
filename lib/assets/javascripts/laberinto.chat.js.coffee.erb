lr = window.lr ||= {}

class User
  constructor: (uid,name) ->
    $.cookie 'uid', uid,
      expires: 365
      path: '/'
    @name = name
    @uid  = uid


class Chat
  constructor: (options) ->
    @input = $(options.input)
    @board = $(options.board)
    @list = $(options.list)
    @socket = options.socket
    @init()

  init: ->
    @input.bind 'keydown', @catchEnter

  catchEnter: (e) =>
    body = @input.val()
    if e.keyCode is 13 and body isnt ''
      nick = @user.name
      message = new ChatMessage(nick, body)
      @renderMessage message
      @socket.emit 'user.speak',
        nick: message.sender
        body:message.body
      @clearInput()
    return

  renderMessage: (msg) ->
    @board.append(msg.$el)
    @board.scrollTop(@board.scrollTop() + msg.$el.height() * 2)

  clearInput: ->
    @input.val('')

  updateList: (users) ->
    @list.empty()
    for id,user of users when user.online is true
      el = $('<p class="user">')
      el.text(user.name)
      el.prepend($('<i class="icon-user">  </i>'))
      @list.append(el)
    return

class ChatMessage
  constructor: (@sender, @body) ->
    @build()

  build: ->
    nick = $('<span class="nick" />').text("#{@sender}: ")
    body = $('<span class="body" />').text(@body)
    @$el = $('<p class="msg">').append(nick, body)
    return

lr.User = User
lr.Chat = Chat
lr.ChatMessage = ChatMessage
